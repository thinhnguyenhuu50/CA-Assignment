\section{Các giải thuật và logic}
\subsection{Nhận tọa độ từ người chơi}
\begin{enumerate}
    \item \textbf{Nhận tọa độ di chuyển của người chơi trong trò chơi Gomoku}: Chương trình thực hiện một quy trình có cấu trúc để đảm bảo rằng đầu vào được định dạng đúng, nằm trong phạm vi hợp lệ và trùng với một vị trí trên bàn cờ chưa bị chiếm.
    \item \textbf{Hiển thị thông báo yêu cầu người chơi}: 
    \begin{itemize}
        \item Đối với Người chơi 1: ``Người chơi 1, vui lòng nhập tọa độ của bạn: ''
        \item Đối với Người chơi 2: ``Người chơi 2, vui lòng nhập tọa độ của bạn: ''
    \end{itemize}
\end{enumerate}

\begin{code}
    # Display prompt based on current player
    li $v0, 4         # Syscall to print string
    beq $s0, 1, print_prompt1
    la $a0, prompt2   # "Player 2, please input your coordinates: "
    j print_prompt
print_prompt1:
    la $a0, prompt1   # "Player 1, please input your coordinates: "
print_prompt:
    syscall
\end{code}

\begin{enumerate}[resume]    
    \item \textbf{Đọc đầu vào}: 
    \begin{itemize}
        \item Chương trình nhận đầu vào từ người dùng dưới dạng chuỗi thông qua một lệnh gọi hệ thống.
        \item Dữ liệu nhận được được lưu vào một bộ đệm để xử lý tiếp.
    \end{itemize}
\end{enumerate}

\begin{code}
    # Read input string
    li $v0, 8         # Syscall to read string
    la $a0, input_buffer
    li $a1, 10        # Max length of input
    syscall
\end{code}

\begin{enumerate}[resume]
    \item \textbf{Phân tích đầu vào}: 
    \begin{itemize}
        \item Phân tách chuỗi để trích xuất tọa độ x và y theo định dạng ``x,y'', trong đó x và y là số nguyên từ 0 đến 14.
        \item \textbf{Trích xuất x}: Đọc các ký tự trước dấu phẩy và kiểm tra rằng chúng đều là chữ số, xây dựng giá trị x bằng cách nhân giá trị hiện tại với 10 và cộng thêm chữ số mới.
        \item \textbf{Bỏ qua dấu phẩy}: Di chuyển qua dấu phẩy để xử lý tiếp phần tọa độ y.
        \item \textbf{Trích xuất y}: Đọc các chữ số sau dấu phẩy cho đến khi gặp ký tự xuống dòng hoặc ký tự kết thúc, xây dựng giá trị y tương tự như x.
        \item Nếu bất kỳ ký tự nào không phải là số hoặc định dạng không đúng (ví dụ, thiếu dấu phẩy), đầu vào sẽ bị xem là không hợp lệ.
    \end{itemize}
\end{enumerate}

\begin{code}
parse_x:
    lb $t0, 0($s0)    # Load current character
    beq $t0, 44, end_parse_x  # If comma (ASCII 44), end x parsing
    beq $t0, 0, invalid  # If null, invalid
    beq $t0, 10, invalid # If newline, invalid

    # Check if character is a digit (0-9)
    blt $t0, 48, invalid  # < '0'
    bgt $t0, 57, invalid  # > '9'

    # Update x = x * 10 + (char - '0')
    mul $s1, $s1, 10  # x *= 10
    sub $t0, $t0, 48  # Convert char to integer
    add $s1, $s1, $t0 # x += digit

    addi $s0, $s0, 1  # Move to next character
    j parse_x

end_parse_x:
    addi $s0, $s0, 1  # Skip comma
    # Step 2: Parse y (digits after comma)
parse_y:
    lb $t0, 0($s0)    # Load current character
    beq $t0, 10, end_parse_y  # If newline, end y parsing
    beq $t0, 0, end_parse_y   # If null, end y parsing

    # Check if character is a digit
    blt $t0, 48, invalid  # < '0'
    bgt $t0, 57, invalid  # > '9'

    # Update y = y * 10 + (char - '0')
    mul $s2, $s2, 10  # y *= 10
    sub $t0, $t0, 48  # Convert char to integer
    add $s2, $s2, $t0 # y += digit

    addi $s0, $s0, 1  # Move to next character
    j parse_y

end_parse_y:
\end{code}

\begin{enumerate}[resume]
    \item \textbf{Kiểm tra tính hợp lệ của tọa độ}:
    \begin{itemize}
        \item \textbf{Kiểm tra phạm vi}: Xác nhận rằng cả x và y đều nằm trong phạm vi từ 0 đến 14, tương ứng với bàn cờ 15x15.
        \item \textbf{Kiểm tra vị trí}: Đảm bảo rằng vị trí tại tọa độ (x, y) trên bàn cờ đang trống.
    \end{itemize}
\end{enumerate}

\begin{code}
    blt $s1, 0, invalid   # x < 0
    bgt $s1, 14, invalid  # x > 14
    blt $s2, 0, invalid   # y < 0
    bgt $s2, 14, invalid  # y > 14

    # Step 4: Check if board position is empty
    get($s1, $s2, $v0)
    bne $v0, empty, invalid  # If not empty, position is occupied

    # Step 5: Valid input, return x, y
    move $v0, $s1         # Return x
    move $v1, $s2         # Return y
    j parse_input_exit
\end{code}

\begin{enumerate}[resume]
    \item \textbf{Xử lý đầu vào không hợp lệ}: Nếu đầu vào không hợp lệ do định dạng sai như ``a,b'' hoặc ``4'', giá trị vượt phạm vi như ``15,2'', hoặc vị trí đã bị chiếm, chương trình sẽ hiển thị thông báo lỗi ``\texttt{invalid input, try again}'' và quay lại bước 1 để yêu cầu người chơi nhập lại.
\end{enumerate}

\begin{code}
    # Check if input is invalid
    li $t0, -1
    beq $v0, $t0, invalid_input
    # Input is valid, return x,y
	# x already in $v0
	# y in $v1
    j get_move_exit

invalid_input:
    # Print error message
    li $v0, 4
    la $a0, error_msg # "Invalid input, try again"
    syscall
    j input_loop      # Re-prompt
\end{code}



\begin{enumerate}[resume]
    \item \textbf{Trả về tọa độ hợp lệ}: Khi đầu vào vượt qua tất cả các kiểm tra, chương trình trả về tọa độ (x, y) để sử dụng trong logic trò chơi, ghi nhận nước đi của người chơi trên bàn cờ.
\end{enumerate}

Quy trình trên đảm bảo rằng chỉ những nước đi hợp lệ được chấp nhận, đáp ứng các yêu cầu của bàn cờ 15x15 và định dạng đầu vào dự kiến, mang lại trải nghiệm người dùng thân thiện và chính xác.

\subsection{Kiểm tra chiến thắng}

\begin{enumerate}
\item \textbf{Tham số đầu vào và kết quả trả về:} Hàm nhận 2 tham số tọa độ x và y của lượt vừa đi của người chơi để kiểm tra điều kiện chiến thắng rồi trả về giá trị true hoặc false.  
\item \textbf{Lưu thanh ghi vào stack trước khi xử lí:} Lưu trữ dữ liệu của các thanh ghi tham số \var{\$a0}, \var{\$a1}, thanh ghi \var{\$ra} và \var{\$s0}, \(\dots\) để sử dụng nhằm tránh mất mát dữ liệu khi gọi hàm.
\end{enumerate}

\begin{code}
addi $sp, $sp, -32
sw $ra, 0($sp)
sw $a0, 4($sp)
sw $a1, 8($sp)
sw $s0, 12($sp)
sw $s1, 16($sp)
sw $s2, 20($sp)
sw $s3, 24($sp)
sw $s4, 28($sp)
\end{code}

\begin{enumerate}[resume] 
    \item \textbf{Lấy giá trị tại tọa độ của bàn cờ:} gọi macro \var{get} để lấy giá trị của bàn cờ tại tọa độ x và y.
\end{enumerate}

\begin{code}
    get($a0, $a1, $s0)
\end{code}

\begin{enumerate}[resume]
    \item \textbf{Kiểm tra điều kiện thắng của trò chơi:} 
    \begin{itemize}
        \item Từ tọa độ của người ta phải kiểm tra 4 hướng chính như sau: \(\searrow \) \(\nearrow\) \(\uparrow\) \(\to \). Hàm sẽ sử dụng vòng lặp để chạy 4 hướng chính sau cũng như tạo sẵn 2 mảng dùng để dễ dàng tăng thêm theo hướng cho tọa độ x, y đang đứng. 
        \item Dùng vòng lặp để chạy cả 2 chiều theo chiều dương và chiều âm của hướng để kiểm tra lần lượt các điều kiện sau để dừng vòng lặp:
        \begin{itemize}
            \item Ô đang đứng có phải là vị trí hợp lệ hay không, tức là kiểm tra \(0 \leq x \leq 14\) và \(0 \leq y \leq 14\)
            \item So sánh giá trị của bàn cờ tại tọa độ đang đứng có bằng với giá trị của người chơi hay không. \textit{Ví dụ}: Hàm có đầu vào ban đầu là \((x_0, y_0)\) có giá trị trên bàn cờ là \textbf{X}, sau khi chạy vòng lặp tới ô (\(x_1, y_1\)) nếu có giá trị bàn cờ là \textbf{X} thì tiếp tục còn là \textbf{O} thì dừng.
        \end{itemize}
        \item Sau khi kiểm tra điều kiện thỏa mãn, ta tăng tổng ô liên tiếp 1 đơn vị và kiểm tra thêm nếu tổng lớn hơn bằng 5 thì ngừng lặp và trả về kết quả true. Nếu không thì tiếp tục vòng lặp. Khi hết mỗi hướng đi thì ta reset lại tổng đó về 1 để đếm lại.
        \item Sau khi hết vòng lặp mà chưa trả về kết quả thì hàm trả về fasle. 
    \end{itemize}
\end{enumerate}

\begin{code}
	#$s0 = Player
	get($a0, $a1, $s0)
	#$s1 = loop count
	li $s1, 4
check_win_for:
	beqz $s1, check_win_exit
	addi $s1, $s1, -1
	#$s2 = count
	li $s2, 1
#Direction (+)
	lw $s3, 4($sp)
	lw $s4, 8($sp)
check_win_loop1:
sll $t1, $s1, 2
la $t0, Dx
add $t0, $t0, $t1
lw $t0, 0($t0)
add $s3, $s3, $t0
la $t0, Dy
add $t0, $t0, $t1 
lw $t0, 0($t0)
add $s4, $s4, $t0

li $t0, 14
blt $t0, $s3, check_win_exit1
blt $t0, $s4, check_win_exit1
bltz $s3, check_win_exit1
bltz $s4, check_win_exit1
get($s3, $s4, $t0)
bne $t0, $s0, check_win_exit1

addi $s2, $s2, 1
li $t0, 4
blt $t0, $s2, check_win_true
j check_win_loop1

check_win_exit1:
#Direction (-)
	lw $s3, 4($sp)
	lw $s4, 8($sp)
check_win_loop2:
sll $t1, $s1, 2
la $t0, Dx
add $t0, $t0, $t1 
lw $t0, 0($t0)
sub $s3, $s3, $t0
la $t0, Dy
add $t0, $t0, $t1 
lw $t0, 0($t0)
sub $s4, $s4, $t0

li $t0, 14
blt $t0, $s3, check_win_exit2
blt $t0, $s4, check_win_exit2
bltz $s3, check_win_exit2
bltz $s4, check_win_exit2
get($s3, $s4, $t0)
bne $t0, $s0, check_win_exit2

addi $s2, $s2, 1
li $t0, 4
blt $t0, $s2, check_win_true
j check_win_loop2

check_win_exit2:
j check_win_for

check_win_exit:
li $v0, 0
j check_win_return

check_win_true:
li $v0, 1
check_win_return:
\end{code}

\begin{enumerate}[resume]
    \item \textbf{Load dữ liệu của thanh ghi trong stack sau khi xử lí:} Load dữ liệu của các thanh ghi \var{\$ra} và \var{\$s0}, \(\dots\) nhằm tránh mất mát dữ liệu khi gọi hàm.
\end{enumerate}
    
\begin{code}
	lw $ra, 0($sp)
	lw $s0, 12($sp)
	lw $s1, 16($sp)
	lw $s2, 20($sp)
	lw $s3, 24($sp)
	lw $s4, 28($sp)
	addi $sp, $sp, 32
\end{code}

\subsection{Kiểm tra kết quả hòa}
Đơn giản hơn nhiều so với kiểm tra chiến thắng, ta chỉ cần so sánh số bước đã đi với kích thước bàn cờ (15 x 15) xem đã bằng nhau hay chưa để xác định kết quả hòa: 
\begin{code}
    beq move_count, 225, tie # 15x15
\end{code}
\subsection{Ghi kết quả trò chơi}
Sau khi đã xác định kết quá của trò chơi (hòa hoặc có người chơi thắng), chương trình thực hiện ghi kết quả ra console và file ``result.txt''. 

\subsubsection*{Ghi kết quả chiến thắng: } Chương trình thực hiện tuần tự các bước sau:
\begin{enumerate}
    \item \textbf{Lưu thanh ghi vào stack:} Để đảm bảo toàn vẹn dữ liệu giống như đã nêu ở phần kiểm tra chiến thắng, chương trình thực hiện lưu lại các thanh ghi như sau:
    \begin{code}
        win_process:
            # Save registers
            addi $sp, $sp, -12
            sw $ra, 0($sp)
            sw $s0, 4($sp)      # for file descriptor
            sw $t0, 8($sp)      # for msg addr
        \end{code}
        Do ta cần dùng thanh ghi \var{\$s0} cho mô tả tập tin, và thêm 1 thanh ghi \var{\$t0} để lưu địa chỉ của thông báo nên chương trình thực hiện lưu thanh ghi \var{\$ra} cùng với 2 thanh ghi này.
    \item \textbf{Xác định người chơi nào thắng cuộc:} 
    \begin{code}
        # Determine winner msg
        bne $s7, 1, p2_wins # $s7 = player
        la $a0, p1_win_msg
        la $t0, p1_win_msg  # store msg addr for later
        j write_result
    p2_wins:
        la $a0, p2_win_msg
        la $t0, p2_win_msg  # store msg addr for later
    \end{code}

    \item \textbf{Ghi kết quả ra console:}
    \begin{code}
write_result:
    # Print winner msg to console
    li $v0, 4
    syscall
    \end{code}
    \item \textbf{Ghi kết quả vào file ``result.txt'':} Ta cần thực hiện các bước: mở file, ghi thông báo người thắng, ghi bàn cờ, đóng file. Các bước trên được thực hiện bằng đoạn chương trình sau:
    \begin{code}
    # Open file "result.txt"
    li $v0, 13
    la $a0, result_file
    li $a1, 9           # Write-only, create, append
    li $a2, 438         # File mode
    syscall
    move $s0, $v0       # Save file descriptor      

    # Write winner msg to file
    li $v0, 15
    move $a0, $s0
    move $a1, $t0       # load msg addr from $t0
    li $a2, 14          # Size of "Player X wins\n"
    syscall

    # Write board to file
    li $v0, 15
    move $a0, $s0
    la $a1, board
    li $a2, 752         # Size of the board
    syscall

    # Close file
    li $v0, 16
    move $a0, $s0
    syscall

    \end{code}
    Do giá trị thanh ghi \var{\$a0} và \var{\$a1} thay đổi liên tục xuyên suốt các bước, nên các thanh ghi \var{\$s0} cũng như \var{\$t0} là cần thiết để chương trình thực thi chính xác.
    
    
    Ngoài ra, khi ghi thông báo vào file, vì cả 2 thông báo ``Player 1 wins\textbackslash n''   và ``Player 2 wins\textbackslash n'' có cùng độ dài 14 ký tự, nên ta chỉ cần load integer 14 vào thanh ghi \(\$a2\) cho cả 2 trường hợp. 
    \item \textbf{Trả dữ liệu của các thanh ghi về như cũ:}
    \begin{code}
    # Restore registers
    lw $t0, 8($sp)       
    lw $s0, 4($sp)
    lw $ra, 0($sp)
    addi $sp, $sp, 12
    jr $ra
    \end{code}
    Sau khi thực hiện xong việc ghi kết quả chiến thắng, chương trình trả dữ liệu của các thanh ghi \var{\$ra}, \var{\$s0}, \var{\$t0}, tiếp tục thực thi những lệnh kế tiếp.
\end{enumerate}
\subsubsection*{Ghi kết quả hòa: } Chương trình thực hiện các bước tương tự như ghi kết quả chiến thắng.